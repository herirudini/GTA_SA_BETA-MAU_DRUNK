// THIS MOD CREATED BY heri hover

// VARIABLES:
// 0@: Integer    // DRUNK LEVEL , ACTUAL MAX = 220, BUT WE USE 200 AS MAX POINT BECAUSE IN THIS MOD WE USE TIMING FOR "TAPPERING OFF" AND CJ CAN "COMBINE" BOTH DRUG AND BEER (DIFFERENT BEHAVIOUR) SO WE NEED TO BE SAFE FOR CALCULATE EVERYTHING AND THEN USE 200 AS OUR MAX POINT, SAY THE MOST POSSIBLE OFFESET CALCULATION WOULDBE 219, AND THE VALUE WILL BE REVERT TO 200, IT IS BETTER THAN IF THE CALCULATION OFFSET OVER 220 AND THE CAMERA RESET TO NORMAL.
// 1@: Integer    // SKY MODE
// 2@: Integer    // TAPPERING OFF DURATION
// 3@: Integer    // CRACK DEALING COUNTS 
// 4@ = bottle
// 5@ = closest_ped_to
// 6@ = CJ current weapon
// 7@ = eating gum
// 8@ = Player.WantedLevel($PLAYER_CHAR)
// 9@ = closest_vehicle_to
// 10@ 11@ 12@ = CJ coordinates point at :STREET   
// 13@ = actor PLAYER current interior
// 14@ = PUKE VOMIT
// 15@ = Player Money
// 16@ 17@ 18@ 19@ 20@ = bar location coordinates
           
{$CLEO} 

03A4: name_thread 'BETA_DRUNK'

:BETA_DRUNK
// delay a bit after game start
0001: wait 2500
// ===== LIL PROB INN =====
03BC: 16@ = create_sphere_at -223.3139 1409.3814 27.7734 radius 0.5
// ===== STRIP CLUB 1 =====
03BC: 17@ = create_sphere_at 1215.366 -13.1901 1000.922 radius 0.5
// ===== DANCE CLUB =====
03BC: 18@ = create_sphere_at 501.8262 -15.2847 1000.6719 radius 0.5
// ===== STRIP CLUB 2 =====
03BC: 19@ = create_sphere_at 1206.2263 -26.5059 1000.9531 radius 0.5
// ===== BAR =====
03BC: 20@ = create_sphere_at 497.7207 -75.908 998.7578 radius 0.5
jump @LOAD

:LOAD
0001: wait 0 ms  
var
    0@: Integer    // DRUNK LEVEL , ACTUAL MAX = 220, BUT WE USE 200 AS MAX POINT BECAUSE IN THIS MOD WE USE TIMING FOR "TAPPERING OFF" AND CJ CAN "COMBINE" BOTH DRUG AND BEER (DIFFERENT BEHAVIOUR) SO WE NEED TO BE SAFE FOR CALCULATE EVERYTHING AND THEN USE 200 AS OUR MAX POINT, SAY THE MOST POSSIBLE OFFESET CALCULATION WOULDBE 219, AND THE VALUE WILL BE REVERT TO 200, IT IS BETTER THAN IF THE CALCULATION OFFSET OVER 220 AND THE CAMERA RESET TO NORMAL.
    1@: Integer    // SKY MODE
    2@: Integer    // TAPPERING OFF DURATION
    3@: Integer    // CRACK DEALING COUNTS  
    15@: Integer // Money 
end
0004: 1@ = 0   
0004: 3@ = 0   
0002: jump @LOAD_0   

:LOAD_0
0001: wait 0 ms
04ED: load_animation "GANGS"
04ED: load_animation "DEALER" 
04ED: load_animation "FOOD"  
0247: load_model 1486  
038B: load_requested_models     
0002: jump @INIT 

:INIT 
0001: wait 0 ms
00D6: if 
04EE:   animation "GANGS" loaded 
jf @LOAD_0
00D6: if 
04EE:   animation "DEALER" loaded 
jf @LOAD_0    
00D6: if 
04EE:   animation "FOOD" loaded 
jf @LOAD_0  
00D6: if 
0248:   model 1486 available
jf @LOAD_0    
0004: 0@ = 0 
0004: 2@ = 1500 // DEFAULT: 1.5 SECOND     
0002: jump @INIT_0

:INIT_0
0001: wait 0 ms
00D6: if 
0256:   player $PLAYER_CHAR defined 
jf @INIT
00D6: if
8118:   not actor $PLAYER_ACTOR dead 
jf @INIT     
15@ = Player.Money($PLAYER_CHAR)  
0002: jump @BAR_1

:BAR_1
0001: wait 0 ms
077E: get_active_interior_to 13@     
00D6: if or
13@ == 17    // DANCE CLUB
13@ == 2     // STRIP CLUB 1
13@ == 3     // STRIP CLUB 2 (OLD VENTURAS)
13@ == 11    // BAR
13@ == 18    // LIL PROB IN
jf @STREET
0002: jump @BAR_2

:BAR_2 
0001: wait 1500 ms  
00D6: if or
00ED:   actor $PLAYER_ACTOR sphere -1 near_point 497.7207 -75.908 radius 0.5 0.5 on_foot // BAR
00ED:   actor $PLAYER_ACTOR sphere -1 near_point -223.31394958 1409.38146973 radius 0.5 0.5 on_foot // LIL PROB INN
00ED:   actor $PLAYER_ACTOR sphere -1 near_point 1215.366 -13.1901 radius 0.5 0.5 on_foot // STRIP CLUB 1
00ED:   actor $PLAYER_ACTOR sphere -1 near_point 501.8262 -15.284752845 radius 0.5 0.5 on_foot // DANCE CLUB
00ED:   actor $PLAYER_ACTOR sphere -1 near_point 1206.22631836 -26.505908966 radius 0.5 0.5 on_foot // STRIP CLUB 2
jf @BAR_1    
Player.CanMove($PLAYER_CHAR, False)
0173: set_actor $PLAYER_ACTOR Z_angle_to 180.0  
067C: put_camera_on_actor $PLAYER_ACTOR offset 0.0 -1.0 0.8 rotation 0.0 1.0 0.7 tilt 0.0 switchstyle 2
0001: wait 500 ms
03E5: show_text_highpriority GXT 'CLOTHC' time 5000 flag 1
0002: jump @BAR_5    

:BAR_5
0001: wait 0 ms 
00D6: if 
00E1:   player 0 pressed_key 16 // SPRINT BUTTON
then
03E6: remove_text_box 'CLOTHC'
    00D6: if 
    15@ < 10
    then
    Player.CanMove($PLAYER_CHAR, True)   
    097A: play_audio_at $TEMPVAR_X_COORD $TEMPVAR_Y_COORD $TEMPVAR_Z_COORD event 1055
    02EB: restore_camera_with_jumpcut 
    00BC: show_text_highpriority GXT 'SHOPNO' time 5000 flag 1     // dont have enough money, cannot buy at this time
    0001: wait 3000 ms
    0002: jump @INIT_0
    else
    0002: jump @BAR_6 
    end
else   
00D6: if 
00E1:   player 0 pressed_key 15 // GET IN CAR BUTTON
jf @BAR_5
Player.CanMove($PLAYER_CHAR, True)   
097A: play_audio_at $TEMPVAR_X_COORD $TEMPVAR_Y_COORD $TEMPVAR_Z_COORD event 1055
02EB: restore_camera_with_jumpcut 
03E6: remove_text_box 'CLOTHC'
0001: wait 3000 ms
0002: jump @BAR_2
end

:BAR_6
0001: wait 0 ms  
0647: AS_actor $PLAYER_ACTOR clear_look_task
0109: player $PLAYER_CHAR money = -10 
097A: play_audio_at $TEMPVAR_X_COORD $TEMPVAR_Y_COORD $TEMPVAR_Z_COORD event 1054   
03BF: set_player $PLAYER_CHAR ignored_by_everyone 1  
029B: 4@ = init_object 1486 at 0.0 0.0 0.0 
0382: set_object 4@ collision_detection 0 
01B9: set_actor $PLAYER_ACTOR armed_weapon_to 0     
0605: actor $PLAYER_ACTOR perform_animation "DRUGS_BUY" IFP "GANGS" framedelta 7.0 loop 0 lockX 0 lockY 0 lockF 0 time 800  
0001: wait 800 ms 
070A: AS_actor $PLAYER_ACTOR attach_to_object 4@ offset 0.06 0.015 -0.05 on_bone 6 16 perform_animation "DRNKBR_PRTL" IFP_file "GANGS" time 2300   
0001: wait 1800 ms 
0249: release_model 1486  
0001: wait 500 ms 
070B: set_actor $PLAYER_ACTOR onbone_attached_object_operation 1       
0108: destroy_object 4@ 
0687: clear_actor $PLAYER_ACTOR task 
03BF: set_player $PLAYER_CHAR ignored_by_everyone 0  
0002: jump @BAR_7

:BAR_7
0001: wait 0 ms 
if 0@ < 190
then
Inc(0@,30)     
0002: jump @DRUNK 
else 
0@ = 200 // MAXIMUM DRUNK LEVEL
Player.CanMove($PLAYER_CHAR, True)   
02EB: restore_camera_with_jumpcut 
gosub @VOMIT  
00BC: show_text_highpriority GXT 'FOOD1' time 5000 flag 1
0001: wait 5000 ms
0851: set_actor $PLAYER_ACTOR decrease_health_by 2 affect_armour 0
0002: jump @DRUNK 
end  

:CHECK
0001: wait 0 ms 
if
0@ > 0
then   
0001: wait 2@ ms  // NOTICE, THIS IS THE DURATION OF CJ's TAPPERING OFF FROM SUBSTANCE
Dec(0@,1) // DECREMENT DRUNK LEVEL 1 
0002: jump @DRUNK 
else  
0002: jump @INIT
end  
  
:DRUNK 
0001: wait 0 ms                  
052C: set_player $PLAYER_CHAR drunk_visuals 0@    
if and
1@ == 1
0@ > 20 
then   
04F9: set_sky_color 1@ fade 0 
0002: jump @INIT_0  
else  
    if
    1@ == 1 
    jf @INIT_0
    0004: 1@ = 0     
    015D: set_gamespeed 1.0
    04FA: reset_sky_colors_with_fade 0
    0002: jump @INIT_0  
end 

:VOMIT
0001: wait 0 ms
Player.CanMove($PLAYER_CHAR, False)
03CF: load_mission_audio 1828 as 4
03BF: set_player $PLAYER_CHAR ignored_by_everyone 1 
0812: AS_actor $PLAYER_ACTOR perform_animation "EAT_VOMIT_P" IFP "FOOD" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time -1 // versionB
0947: actor $PLAYER_ACTOR speak_from_audio_table 353 store_spoken_phrase_id_to $2563  
0001: wait 3900 ms
04C4: store_coords_to 10@ 11@ 12@ from_actor $PLAYER_ACTOR with_offset 0.0 0.0 0.0
10@ -= 0.360 
11@ += 0.130 
12@ -= 0.200 
064B: 14@ = create_fx_system "PUKE" at 10@ 11@ 12@ type 1 
064C: play_fx_system 14@
if
03D0: has_mission_audio_loaded 4
then
09F1: report_mission_audio_event_at_char $PLAYER_ACTOR event 1169
end
0001: wait 1500 ms
064E: stop_fx_system 14@
0650: kill_fx_system 14@
03BF: set_player $PLAYER_CHAR ignored_by_everyone 0
Player.CanMove($PLAYER_CHAR, True)
return

:STREET  
0001: wait 0 ms  
00D6: if
13@ == 0 // Free World
jf @INIT_0
00D6: if 
not Actor.Driving($PLAYER_ACTOR)
004D: jump_if_false @CHECK        
0AB5: store_char $PLAYER_ACTOR closest_vehicle_to 9@ closest_ped_to 5@
00D6: if 
056D: actor 5@ defined 
004D: jump_if_false @CHECK
00D6: if 
not Actor.Driving(5@)
004D: jump_if_false @CHECK   
00D6: if 
8118:   not actor 5@ dead 
004D: jump_if_false @CHECK 
00D6: if
00F3:   actor $PLAYER_ACTOR near_actor 5@ radius 4.9 4.9 sphere 0 on_foot
004D: jump_if_false @CHECK   
00D6: if or
02F2:   actor 5@ model == #BMYDRUG
02F2:   actor 5@ model == #WMYDRUG 
02F2:   actor 5@ model == #HMYDRUG 
02F2:   actor 5@ model == #BIKDRUG
then 
    00D6: if and
    02F2:   actor 5@ model == #BMYDRUG
    0038:   $ONMISSION == 1 //boolean
    $SWEET_TOTAL_PASSED_MISSIONS == 1
    then    
    jump @CHECK   
    end
else 
jump @CHECK 
end
00D6: if or
074F:   is_char_responding_to_event 5@ event 31 //being aimed by CJ
074F:   is_char_responding_to_event 5@ event 36 //Ped Hate DEAL3
074F:   is_char_responding_to_event 5@ event 9 //damaged
074F:   is_char_responding_to_event 5@ event 65 //EVENT_SEEN_PANICKED_PED
074F:   is_char_responding_to_event 5@ event 63 //Hate Badly
074F:   is_char_responding_to_event 5@ event 50 //Angry at player
074F:   is_char_responding_to_event 5@ event 51 //Angry at player
then
jump @CHECK
end
if or
  has_char_been_damaged_by_char 5@ damaged_by_actor $PLAYER_ACTOR
  is_player_targetting_char $PLAYER_CHAR aiming_at_actor 5@
then
jump @CHECK
end
Actor.StorePos($PLAYER_ACTOR, 20@, 21@, 22@)
get_random_char_in_sphere_only_drugs_buyers 20@ 21@ 22@ radius 5.0 handle_as 23@
if 
  23@ == -1
goto_if_false @CHECK
00D6: if and
09F2:   does_decision_maker_exist $9453
089B:   is_player_in_position_for_conversation 5@
004D: jump_if_false @CHECK
07BC: task_set_char_decision_maker 5@ to $9453    
00D6: if or
071A:   is_conversation_at_node 5@ current_dialogue_text == 'DEAL1' // YOU WANNA SOME? 
071A:   is_conversation_at_node 5@ current_dialogue_text == 'DEAL2' // TAKE IT EASY MAN  
then
00D6: if 
071A:   is_conversation_at_node 5@ current_dialogue_text == 'DEAL2' // TAKE IT EASY MAN
then 
0A09: set_actor 5@ muted 1
end
0459: end_script_named 'DEALER' // IMPORTANT! THIS IS NEEDED TO STOP THREAD NAMED :DEALER FROM main.scm 
03E6: remove_text_box 'TALK_1' 
0A1D: AS_actor 5@ rotate_to_and_look_at_actor $PLAYER_ACTOR  
0605: actor 5@ perform_animation "DEALER_IDLE" IFP "DEALER" framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time -1 
0A09: set_actor $PLAYER_ACTOR muted 1 
03E5: show_text_highpriority GXT 'TALK_1' time 5000 flag 1 
jump @TALK
else
0459: end_script_named 'DEALER' // IMPORTANT! THIS IS NEEDED TO STOP THREAD NAMED :DEALER FROM main.scm       
jump @TALK_0 // Will be straight to DEAL3 condition
end   

:TALK  
0001: wait 0 ms   
00D6: if or
074F:  actor 5@ ped_event == 9  // Damaged
074F:  actor 5@ ped_event == 65
then
03E6: remove_text_box 'TALK_1'
0A09: set_actor $PLAYER_ACTOR muted 0
004D: jump @CHECK
else
end  
00D6: if
00E1:   player 0 pressed_key 11 //Ginput Right 
then 
03E6: remove_text_box 'TALK_1'                  
else
004D: jump @TALK_0
end 
00D6: if 
15@ > 10
then
0639: task_turn_char_to_face_char 5@ char $PLAYER_ACTOR
0605: actor 5@ perform_animation "DEALER_IDLE" IFP "DEALER" framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time -1
else
00BC: show_text_highpriority GXT 'SHOPNO' time 5000 flag 1     // dont have enough money, cannot buy at this time   
0001: wait 5000 ms
004D: jump @CHECK
end
00D6: if
8118:   not actor 5@ dead 
then  
else
0A09: set_actor $PLAYER_ACTOR muted 0
jump @STREET
end  
jump @BUY

:TALK_0 // CHECK IF CJ RESPONSE ~NO~ OR JUST IGNORE THE DEALER
0001: wait 0 ms 
00D6: if 
not Actor.Driving($PLAYER_ACTOR)
then   
else
0A09: set_actor $PLAYER_ACTOR muted 0 
03E6: remove_text_box 'TALK_1' 
jump @CHECK 
end
00D6: if
00F3:   actor $PLAYER_ACTOR near_actor 5@ radius 4.9 4.9 sphere 0 on_foot
then
0470: 6@ = actor $PLAYER_ACTOR current_weapon
else 
0A09: set_actor $PLAYER_ACTOR muted 0     
03E6: remove_text_box 'TALK_1' 
jump @CHECK 
end 
if or
00E1:   player 0 pressed_key 10  // Ginput Left Pad 
071A:   is_conversation_at_node 5@ current_dialogue_text == 'DEAL3' // FUCK YOU MAN 
then  
03E6: remove_text_box 'TALK_1' 
0A09: set_actor $PLAYER_ACTOR muted 0
05C1: AS_actor $PLAYER_ACTOR speak_from_audio_table 110
05BF: AS_actor $PLAYER_ACTOR look_at_actor 5@ 100 ms
0708: clear_char_decision_maker_event_response $9453 event 36 // reset 0709: event decision Hate
0708: clear_char_decision_maker_event_response $9453 event 37 // reset 0709: event decision Dislike
077A: set_actor 5@ acquaintance 4 to_actors_pedtype 0 // 4 = Dislike
    if 
    6@ > 0 
    then
        0001: wait 0 ms
        01B9: set_current_char_weapon 5@ armed_weapon_to 22
        if
        071A:   is_conversation_at_node 5@ current_dialogue_text == 'DEAL3' // FUCK YOU MAN
        then
        //1020 = TASK_SIMPLE_GUN_CTRL
        //1000 = TASK_COMPLEX_KILL_PED_ON_FOOT
        0709: set_decision_maker $9453 on_event 36 taskID 1000 respect 0.0 hate 100.0 like 0.0 dislike 0.0 in_car 1 on_foot 1
        else
        0709: set_decision_maker $9453 on_event 37 taskID 1020 respect 0.0 hate 100.0 like 0.0 dislike 0.0 in_car 1 on_foot 1
        end
    else
    01B9: set_current_char_weapon 5@ armed_weapon_to 0
    0709: set_decision_maker $9453 on_event 36 taskID 1000 respect 0.0 hate 100.0 like 0.0 dislike 0.0 in_car 1 on_foot 1
    end  
    0001: wait 500 ms
    jump @INIT_0       
else 
jump @TALK
end

:BUY 
0001: wait 500 ms    
gosub @DEAL_READY
0A09: set_actor $PLAYER_ACTOR muted 0
05C1: AS_actor $PLAYER_ACTOR speak_from_audio_table 194 
06A8: AS_actor $PLAYER_ACTOR run_to_and_look_at_actor 5@ timelimit -1 approach_distance 0.7 approach_angle 0.0
0001: wait 1000 ms                                 
0812: AS_actor $PLAYER_ACTOR perform_animation "SHOP_PAY" IFP "DEALER" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time 3000
0001: wait 3000 ms
00D6: if or
074F:   actor 5@ ped_event == 9
074F:   actor 5@ ped_event == 65
then
gosub @DEAL_END
jump @CHECK
else
end
00D6: if
00F3:   actor $PLAYER_ACTOR near_actor 5@ radius 2.0 2.0 sphere 0 on_foot  // FIX: cancel buy if the dealer run away
then
0109: player $PLAYER_CHAR money = -20
0687: clear_actor 5@ task    
0812: AS_actor 5@ perform_animation "DEALER_DEAL" IFP "DEALER" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time 3200
0812: AS_actor $PLAYER_ACTOR perform_animation "DRUGS_BUY" IFP "DEALER" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 time 3200
0A09: set_actor 5@ muted 0
05C1: AS_actor 5@ speak_from_audio_table 84  
0001: wait 3000 ms         
gosub @DEAL_END
09B6: set_actor 5@ wanted_by_police 1    
jump @HIT 
else
gosub @DEAL_END
jump @CHECK
end

:HIT
0001: wait 100 ms
0605: actor $PLAYER_ACTOR perform_animation_sequence "gum_eat" from_file "PED"  4.0  0  0  0  0 5000 ms
0001: wait 7000 
0851: set_actor $PLAYER_ACTOR decrease_health_by 1 affect_armour 0 
jump @HIT_0

:HIT_0
0001: wait 0 ms 
if 3@ < 2
then
Inc(3@,1)
jump @HIT_1
else 
13@ = Player.WantedLevel($PLAYER_CHAR)
Inc(13@,1)  
010D: set_player $PLAYER_CHAR wanted_level_to 8@
0004: 3@ = 0
jump @HIT_1
end

:HIT_1
0001: wait 0 ms 
1@ = 1
if 0@ < 100
then
Inc(0@,100)  
jump @DRUNK
else 
0@ = 200 // MAXIMUM DRUNK LEVEL
2@ = 1000 // DECREASE DURATION BECAUSE GAME SPEED WILL BE REDUCED AND CJ's DRUNK TOO LONG (just my preference)  
015D: set_gamespeed 0.9  
jump @DRUNK
end

:DEAL_READY
0001: wait 0 ms 
0350: set_actor 5@ maintain_position_when_attacked 1 
0446: set_actor 5@ dismemberment_possible 0 
0568: set_actor 5@ untargetable 1 
02AB: set_actor 5@ immunities BP 1 FP 1 EP 1 CP 1 MP 1    
01B9: set_actor $PLAYER_ACTOR armed_weapon_to 0   
Player.CanMove($PLAYER_CHAR, False)
03BF: set_player $PLAYER_CHAR ignored_by_everyone 1
return

:DEAL_END
0001: wait 0 ms
03BF: set_player $PLAYER_CHAR ignored_by_everyone 0
0350: set_actor 5@ maintain_position_when_attacked 0 
0446: set_actor 5@ dismemberment_possible 1
0568: set_actor 5@ untargetable 0 
02AB: set_actor 5@ immunities BP 0 FP 0 EP 0 CP 0 MP 0
01B9: set_actor 5@ armed_weapon_to 0
Player.CanMove($PLAYER_CHAR, True)
077A: set_actor 5@ acquaintance 1 to_actors_pedtype 0  //Like or respect
0709: set_decision_maker $9453 on_event 39 taskID 276 respect 100.0 hate 0.0 like 0.0 dislike 0.0 in_car 1 on_foot 1
0605: actor 5@ perform_animation "DEALER_IDLE" IFP "DEALER" framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time 5000
0001: wait 1000 ms
Actor.RemoveReferences(5@)
return